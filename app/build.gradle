apply plugin: 'com.android.application'

static def gitLatestTag() {
    try {
        def cmd = 'git describe --abbrev=0 --tags'
        def result = cmd.execute().text.trim()
        return result
    } catch (Exception e) {
        return '1.0.0'
    }
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.moko.beaconxplus"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName gitLatestTag()

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }

    signingConfigs {
        debug {
            v1SigningEnabled true
            v2SigningEnabled true
        }
        release {
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def formatedDate = new Date().format("yyyyMMddHHmm")
                def apkFileName = "BeaconXPlus_${defaultConfig.versionName}_${formatedDate}.apk"
                if (outputFile.name.contains('debug')) {
                    apkFileName = "BeaconXPlus_${defaultConfig.versionName}_${formatedDate}_debug.apk"
                }
                outputFileName = apkFileName
            }
        }
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion rootProject.v4Version
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:support-v4:$rootProject.v4Version"
    implementation "com.android.support:recyclerview-v7:$rootProject.v4Version"
    implementation "com.android.support:appcompat-v7:$rootProject.v4Version"
    implementation 'com.jakewharton:butterknife:7.0.1'
    implementation 'com.github.GrenderG:Toasty:1.4.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.30'
    implementation 'cn.carbswang.android:NumberPickerView:1.1.1'
    implementation project(path: ':mokosupport')
}
